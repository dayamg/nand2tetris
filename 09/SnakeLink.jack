

/** Implements a graphical snakeLink. */
class SnakeLink {

   field int xLoc, yLoc; // screen location of the snakeLink's top-left corner
   field int size; // length of this snakeLink, in pixels
   field boolean isHead; // indicates in snake link is the head, for drowing purpose.

   /** Constructs a new snakeLink with a given location and size. */
   constructor SnakeLink new(int Ax, int Ay, int Asize) {
	  let xLoc = Ax;
	  let yLoc = Ay;
	  let size = Asize;
	  do draw();
	  return this;
   }

  /** xLoc getter */
   method int xLocGetter() {
	  return xLoc;
   }
   
  /** yLoc getter */
   method int yLocGetter() {
	  return yLoc;
   }  
  
  /** xLoc setter */
   method void xLocSetter(int val) {
	  let xLoc = val;
	  return;
   }
  
	/** yLoc setter */
   method void yLocSetter(int val) {
	  let yLoc = val;
	  return;
   }
  
 
   /** Disposes this SnakeLink. */
   method void dispose() {
	  do Memory.deAlloc(this);
	  return;
   }

   /** Draws the SnakeLink on the screen. */
   method void draw() {
	   	var int location;
		let location = ((32*yLoc) + (xLoc/16));
		do SnakeLink.draw16TiltLeft(location);
		let location = ((32*yLoc) + ((xLoc+16)/16));
		do SnakeLink.draw16TiltLeft(location);
		let location = ((32*(yLoc+16)) + (xLoc/16));
		do SnakeLink.draw16TiltRight(location);		
		let location = ((32*(yLoc+16)) + ((xLoc+16)/16));
		do SnakeLink.draw16TiltRight(location);	
		return;		
   }		
   
   /** Draws 1/4 of the SnakeLink on the screen, with pattern of tilt right lines. */
   function void draw16TiltRight(int location) {
		var int memAddress;	
		let memAddress = 16384 + location;
		do Memory.poke(memAddress + 0, 3855);
		do Memory.poke(memAddress + 32, -30841);
		do Memory.poke(memAddress + 64, -15421);
		do Memory.poke(memAddress + 96, -7711);
		do Memory.poke(memAddress + 128, -3856);
		do Memory.poke(memAddress + 160, 30840);
		do Memory.poke(memAddress + 192, 15420);
		do Memory.poke(memAddress + 224, 7710);
		do Memory.poke(memAddress + 256, 3855);
		do Memory.poke(memAddress + 288, -30841);
		do Memory.poke(memAddress + 320, -15421);
		do Memory.poke(memAddress + 352, -7711);
		do Memory.poke(memAddress + 384, -3856);
		do Memory.poke(memAddress + 416, 30840);
		do Memory.poke(memAddress + 448, 15420);
		do Memory.poke(memAddress + 480, 7710);
		return;
   }

 /** Draws 1/4 of the SnakeLink on the screen, with pattern of tilt left lines. */
   function void draw16TiltLeft(int location) {
		var int memAddress;	
		let memAddress = 16384 + location;
		do Memory.poke(memAddress + 0, 7710);
		do Memory.poke(memAddress + 32, 15420);
		do Memory.poke(memAddress + 64, 30840);
		do Memory.poke(memAddress + 96, -3856);
		do Memory.poke(memAddress + 128, -7711);
		do Memory.poke(memAddress + 160, -15421);
		do Memory.poke(memAddress + 192, -30841);
		do Memory.poke(memAddress + 224, 3855);
		do Memory.poke(memAddress + 256, 7710);
		do Memory.poke(memAddress + 288, 15420);
		do Memory.poke(memAddress + 320, 30840);
		do Memory.poke(memAddress + 352, -3856);
		do Memory.poke(memAddress + 384, -7711);
		do Memory.poke(memAddress + 416, -15421);
		do Memory.poke(memAddress + 448, -30841);
		do Memory.poke(memAddress + 480, 3855);
		return;
   }

   
   /** Erases the SnakeLink from the screen. */
   method void erase() {
	  do Screen.setColor(false);
	  do Screen.drawRectangle(xLoc, yLoc, xLoc + size-1, yLoc + size-1);
	  return;
   }
   
   /** Move the SnakeLink to given location on the screen. */
   method void moveTo(int NewXLoc, int NewYLoc) {
	  let xLoc = NewXLoc;
	  let yLoc = NewYLoc;
	  do draw();
	  return;
   }
   
   /** Moves the SnakeLink up by a link. */
	method void moveUp() {
		// drow the link up.
		var int newYloc;
		let newYloc = yLoc - size;
		if (newYloc < 0) {
			let newYloc = (newYloc + 256);
		}
		if (newYloc > (256 - size)) {
			let newYloc = (newYloc - 256);
		}
		do moveTo(xLoc, newYloc);
		return;
	}

   /** Moves the SnakeLink down by a link. */
   method void moveDown() {
		// drow the link down.
		var int newYloc;
		let newYloc = yLoc + size;
		if (newYloc < 0) {
			let newYloc = (newYloc + 256);
		}
		if (newYloc > (256 - size)) {
			let newYloc = (newYloc - 256);
		}
		do moveTo(xLoc, newYloc);
		return;
   }

   /** Moves the SnakeLink left by a link. */
   method void moveLeft() {
		// drow the link left.
		var int newXloc;
		let newXloc = xLoc - size;
		if (newXloc < 0) {
			let newXloc = (newXloc + 512);
		}
		if (newXloc > (512 - size)) {
			let newXloc = (newXloc - 512);
		}
		do moveTo(newXloc, yLoc);
		return;
   }

   /** Moves the SnakeLink right by a link. */
   method void moveRight() {
		// drow the link right.
		var int newXloc;
		let newXloc = xLoc + size;
		if (newXloc < 0) {
			let newXloc = (newXloc + 512);
		}
		if (newXloc > (512 - size)) {
			let newXloc = (newXloc - 512);
		}
		do moveTo(newXloc, yLoc);
		return;
   }   
   
}
